(ns doc
  (:require
   [babashka.fs :as fs]
   [babashka.process :refer [shell]]
   [clojure.string :as str]
   [dinodoc.api :as dinodoc]
   [dinodoc.impl.tbls :as tbls]))

(defn generate [{:keys [dsn output-path]}]
  (try
    (shell "tbls" "doc" "--dsn" dsn "--rm-dist")
    ;; Uncomment to use Mermaid for diagrams:
    ; "--er-format" "mermaid")

    ;; tbls seems to have `docPath` option to use different output path than `dbdoc`.
    ;; But it seems to be only available when using yaml config but not passable via CLI,
    ;; so let's move the output dir ourselves afterwards.
    (fs/delete-tree output-path)
    (fs/create-dirs (fs/parent output-path))
    (fs/move "dbdoc" output-path)

    (fs/update-file
     (str output-path "/README.md")
     #(str "---\nsidebar_position: 0\n---\n\n" %))

    ;; Strip out distracting tbls footer, credits are in the readme
    (doseq [file (fs/glob output-path "*.md")]
      (fs/update-file
       (fs/file file)
       str/replace "---\n\n> Generated by [tbls](https://github.com/k1LoW/tbls)" ""))

    (catch java.io.IOException e
      (println "Something went wrong:")
      (println (ex-message e))
      (println)
      (println "Please make sure you have `tbls` program installed."))))

(defn with-temporary-postgres-db [f]
  (fs/with-temp-dir [dir {}]
    (let [data-dir (str dir "/data")
          db-name "mydb"
          uri (str "postgresql://" dir "/" db-name)]
      (shell "initdb -D" data-dir)
      (shell "pg_ctl start -D" data-dir "-o" "-h ''" "-o" "-k" "-o" dir)
      (shell "createdb -h" dir db-name)
      (try
        (f {:host dir :db db-name :uri uri})
        (finally
          (shell "pg_ctl stop -D" data-dir))))))

;; SQLite Chinook example
(generate {:dsn "sqlite:./chinook/ChinookDatabase/DataSources/Chinook_Sqlite.sqlite"
           :output-path "tmp/chinook"})

;; PostgreSQL Sakila example
(let [output-path "tmp/sakila"]
  (with-temporary-postgres-db
    (fn [{:keys [host db uri]}]
      (shell "psql --quiet -f" "pagila/pagila-schema.sql"
             "-h" host "-d" db)
      (generate {:dsn uri
                 :output-path output-path})))

  ;; Update index page title from `tmp`
  (fs/update-file (str output-path "/README.md")
                  str/replace "# tmp\n" "# Sakila\n"))

(dinodoc/generate
 {:inputs [{:path "."}]
  :output-path "docs"
  :resolve-apilink #(tbls/resolve-link "tmp" %)})
(fs/move "tmp/chinook" "docs")
(fs/move "tmp/sakila" "docs")

(shutdown-agents)
